<h3>4 Developing Slix: A Quick Tour</h3>

<p>This section describes the overview of developing slix. We create a slix called <code>cc</code>, a slix port of <a href="http://clojure.org/jvm_hosted/">a Celsius converter</a>.</p>

<h4>4.1 The <code>create-slix</code> Function</h4>

<p>As explained in the section "2 Getting Started", a slix is an aggregate of libs providing definitions for particular Sevenri application. Each slix has a main lib whose namespace name consists of the prefix <code>"slix."</code> and the slix name. For example, the slix <code>sevenri</code> has the main lib <code>slix.sevenri</code>.</p>

<p>You create the main lib of the slix <code>cc</code> using the <code>create-slix</code> function. Given a slix name the <code>create-slix</code> function writes the main lib file filled with startup code. Open the REPL and call the function like this:</p>

<pre><code>    (create-slix 'cc)
</code></pre>

<p>Notice that the Sevenri now shows the slix name <code>cc</code> in the left pane. You can open <code>cc</code> right away.</p>

<p>"Opening a slix" creates an instance of the slix. a slix instance is a map that represents a slix as executable entity and contains values required to execute the slix functions. Each slix instance has a unique name, normally generated by Sevenri automatically, and a JFrame that is used as the main window of the slix instance.</p>

<p>Double-click <code>cc</code> in the Sevenri to open it. An instance of <code>cc</code> is created and the main window opens. The instance name is displayed in the window title.</p>

<p><img src="../res/ss-dev-slix1.png" alt="Opening cc" title="Opening cc" /></p>

<p>Before we move on and take a look at the main lib file, let's see some Sevenri functions that you use to control slix instance.</p>

<h4>4.2 Opening, Closing, Saving, and Deleting Slix Instance</h4>

<p>You can open slix at the REPL using the <code>open-slix</code> function like this:</p>

<pre><code>    (open-slix 'cc)
</code></pre>

<p>This does the same thing as you double-click <code>cc</code> in the Sevenri. The slix instance name is auto-generated by Sevenri using a naming convention in this format.</p>

<pre><code>*Slix-name-with-initial-capital-letter*[Optinal-number-starting-from-1]
</code></pre>

<p>For examle, the very first instance of <code>cc</code> will be named "Cc", the second instance is "Cc1", the third instance is "Cc2", and so on.</p>

<p>You can also open slix with instance name you like. Specify the instance name following a slix name like this:</p>

<pre><code>    (open-slix 'cc "Celsius Converter")
</code></pre>

<h4>Bear in mind, though, that Sevenri identifies slix instance by instance name and instance name only. So each slix instance name should be unique throughout all slix instances. This means, for example, you cannot open the slix cc with instance name "Celsius Converter" when another slix instance named "Celsius Converter" is running and it doesn't matter whether the running instance is the one of cc or any other slix.</h4>

<p><img src="../res/ss-dev-slix2.png" alt="Opening cc with name" title="Opening cc with name" /></p>

<p>Likewise, you can close slix instance using the <code>close-slix</code> function with slix instance name like this:</p>

<pre><code>    (close-slix "Celsius Converter")
</code></pre>

<p>When closing slix instance, Sevenri gives a chance to save instance data to the slix. Sevenri also saves the JFrame of slix instance and loads it back when the slix with the same instance name is opened. So, when you open "Celsius Converter" again, the window will open at the same location where it was there last time because the window location is a part of saved JFrame data. When slix instance is running, you can cause the save manually using the <code>save-slix</code> function. Like the <code>close-slix</code> function, call the <code>save-slix</code> function with slix instance name.</p>

<p>OK, let's delete the slix instance "Celsius Converter". "Deleting slix instance" means 1) if the slix instance is running, close it, and 2) remove the saved instance file(s) if any. Note that it doesn't mean to delete the slix (the slix lib files). You use the <code>delete-slix</code> function to delete slix instance. The followings open "Celsius Converter" and then delete it.</p>

<pre><code>    (open-slix 'cc "Celsius Converter")
    (delete-slix "Celsius Converter")
</code></pre>

<p>After working with various slixes, you would have bunch of saved slix instances and some of them may become no longer necessary. You can use the <code>delete-slix</code> function to get rid of them too. Suppose you have just closed "Celsius Converter". To delete it call the <code>delete-slix</code> function with slix and instance names like this:</p>

<pre><code>    (delete-slix 'cc "Celsius Converter")
</code></pre>

<p>To get the names of saved slix instances, call the <code>get-saved-slix-names</code> function with slix name. For example:</p>

<pre><code>    (get-saved-slix-names 'cc)
</code></pre>

<h4>4.3 The Main Lib File and *slix*</h4>

<p>From the Sevenri you can not only open slix but also open the main lib file of the slix in Ced. To open it, hold down the META key and double-click slix name.</p>

<p>Open the main lib file of <code>cc</code>. As you can see, the initial, auto-generated startup code is very simple.</p>

<pre><code>    1: (ns ^{:slix true}
    2:   slix.cc
    3:   (:use [sevenri config core event log slix ui utils]))
    4:
    5: (defn opened
    6:   [event]
    7:   (set-slix-visible))
</code></pre>

<p>Let's take a look at the code line by line.</p>

<p>Line 1: The <code>ns</code> macro creates the namespace of the slix. Notice that metadata signifying it is a slix namespace is attached to the namespace var. </p>

<p>Line 2: The namespace name of this slix</p>

<p>Line 3: Refer to the Sevenri libs you would require often.</p>

<p>Line 5: When opening slix, Sevenri notifies open events to the slix through the event handling functions defined in the slix. The <code>opened</code> function is one of them. Sevenri calls the function when slix instance is initialized and the JFrame of the instance is ready to become visible.</p>

<p>Line 6: The <code>opened</code> function takes one argument, which conveys various event information.</p>

<p>Line 7: Whenever Sevenri calls the event handling functions, the current slix instance is set to the <em><code>*slix*</code></em> var. The `set-slix-visible function is a Sevenri function that refers to *slix* by default and makes its JFrame visible.</p>

<h4>4.4 Making Changes</h4>

<p>Let's start porting the Celsius Converter code into the slix <code>cc</code>. First, import the necessary Swing classes. You don't need to import JFrame because it's available in the main lib already (but it makes no harm to import it). The <code>ns</code> macro lines should look like below:</p>

<pre><code>    (ns ^{:slix true}
      slix.cc
      (:use [sevenri config core event log slix ui utils])
      (import (javax.swing JLabel JTextField JButton)
              (java.awt.event ActionListener)
              (java.awt GridLayout)))
</code></pre>

<p>Now let's take a look at the class documentation of, say, <code>GridLayout</code> to see what it is. Put the caret on or next to the string "GridLayout" and press F1. The documentation page will open in a browser (if not, refer to the API-browser documentation and check your network connection).</p>

<p>In the original Celsius Converter code, a JFrame is created in the <code>celsius</code> function. But it is not necessary in slix code because Sevenri creates it for slix. The <code>celsius</code> function of <code>cc</code>, thus, looks slightly different.</p>

<pre><code>    (defn celsius
      [frame]
      (let [temp-text (JTextField.)
            celsius-label (JLabel. "Celsius")
            convert-button (JButton. "Convert")
            fahrenheit-label (JLabel. "Fahrenheit")]
        (.addActionListener convert-button
          (proxy [ActionListener] []
            (actionPerformed [evt]
              (let [c (Double/parseDouble (.getText temp-text))]
                (.setText fahrenheit-label
                   (str (+ 32 (* 1.8 c)) " Fahrenheit"))))))
        (doto frame
          (.setLayout (GridLayout. 2 2 3 3))
          (.add temp-text)
          (.add celsius-label)
          (.add convert-button)
          (.add fahrenheit-label)
          (.setSize 300 80))))
</code></pre>

<p>This function takes JFrame as argument and returns without making it visible, which is done by the <code>set-slix-visible</code> function later. So the only remaining thing to do is to get the JFrame of the current slix instance and call this function in the <code>opened</code> function.</p>

<p>To get the JFrame of the current slix instance you call the <code>slix-frame</code> function. Like the <code>set-slix-visible</code> function, it refers to *slix* by default and returns the JFrame. Make changes to the <code>opened</code> function to get it look like this:</p>

<pre><code>    (defn opened
      [event]
      (celsius (slix-frame))
      (set-slix-visible))
</code></pre>

<p>The port now looks completed. Let's see whether it really works. Press META+S to save the file and then press F2 to load (and compile) it. Nothing should happen if there is no load/compile error.</p>

<p>Let's make an "error" and see what happens if it was there. Remove the slash character '/' of "Double/parseDouble" in the <code>celsius</code> function, save the file and then press F2. This time an Exceptor window with an error message opens in the bottom-right corner of the display. The error message is pointing out the cause, the file name, and the line number. Bring the Ced window editing the file to the front. Make sure to click in the window title, not in the text pane, so that you can see the caret is set at the beginning of the error line. Close the Exceptor window, fix the error and then load the file using F2. Again, you should see no Exceptor window, which means no load/compiler error.</p>

<p>Let's make one final touch; select the whole lines of the <code>celsius</code> function and press TAB to reindent the lines. You can even select the whole lines in the file by pressing META+A and reindent them all, if that's what you really want to do.</p>

<p>When the file currently editing is a slix lib file, Ced can figure out the slix name and open the slix by pressing F5. Do it now and play with your first slix application!</p>

<p><img src="../res/ss-dev-slix3.png" alt="Making changes" title="Making changes" /></p>

<h4>4.5 The Frame.xml File and The event-response-donot-save Macro</h4>

<p>The second time and after when you open <code>cc</code>, you will notice there is something wrong; the UI components get duplicated every time you open <code>cc</code>. </p>

<p><img src="../res/ss-dev-slix4.png" alt="cc with bug" title="cc with bug" /></p>

<p>To understand what is going on, first delete all instances of <code>cc</code> using the <code>delete-slix</code> function. Then open <code>cc</code> from the Sevenri (so the instance has the name "Cc") and close it right away. Now you have one saved instance. In this case only the JFrame of "Cc" is saved. It is saved to the file <code>frame.xml</code> in an XML format. Open the file using the <code>browse-frame-xml</code> function and take a look at the content. To do that, call the function at the REPL like this:</p>

<pre><code>    (browse-frame-xml 'cc 'Cc)
</code></pre>

<p>The <code>frame.xml</code> file will be opened in a browser (or an application associated with the <code>xml</code> extension. On my Mac OS X, for example, <code>Dashcode</code>).</p>

<p><img src="../res/ss-dev-slix5.png" alt="Browsing frame.xml" title="Browsing frame.xml" /></p>

<p>You can also dump the content at the REPL by calling the <code>cat-frame-xml</code> function with the same arguments. In the <code>frame.xml</code> file you can see the UI components created by the <code>celsius</code> function in a serialized form and they all look reasonable; e.g. there are one JTextField for input and two JLabels for displaying the strings "Celsius" and "Fahrenheit". Once again open <code>cc</code>, close it, and then take a look at the content of the XML file. You will now see those UI components are duplicated as you saw them in the window.</p>

<p>That is because, as explained in the section "4.2 Opening, Closing, Saving, and Deleting Slix Instance" above, Sevenri loads the saved JFrame back when opening "Cc" the second time and after. And the <code>opened</code> function is called after loading the saved JFrame so that the same UI components get added in addition to the persisting ones. Hence you see duplicates. There are three ways to solve this issue.</p>

<ol>
<li>Don't save instance. Recreate window content every time when opening.</li>
<li>Don't add the UI components if persisting ones exist.</li>
<li>Add them only when JFrame is created at the very first time.</li>
</ol>

<p>Solution #2 and #3 require somewhat advanced knowledges, like how to reinstall ActionLister created by the <code>proxy</code> function and suppressing it from persisting in the <code>frame.xml</code> file. So we don't pursue them here.</p>

<p>Solution #1 is very easy to implement; just add the event handling function below.</p>

<pre><code>    (defn saving
      [event]
      (event-response-donot-save))
</code></pre>

<p>Sevenri calls the <code>saving</code> function when it's time to save instance data. In return you can tell Sevenri not to save JFrame by returning the value of the <code>event-response-donot-save</code> macro. Delete all saved instances before you open <code>cc</code> with the new function.</p>

<h4>4.6 The frame-created and frame-loaded Functions</h4>

<p>The <code>frame-created</code> and <code>frame-loaded</code> functions are the event handling functions you define in the main lib of slix. Sevenri calls the <code>frame-created</code> function when JFrame is created for slix instance. Likewise, Sevenri calls the <code>frame-loaded</code> function when saved JFrame is loaded.</p>

<p>Let's clean up the <code>cc</code> code using the <code>frame-created</code> function and modify the <code>celsius</code> function to return conversion result with unit name in vector.</p>

<pre><code>    (ns ^{:slix true}
      slix.cc
      (:use [sevenri config core event log slix ui utils])
      (import (javax.swing JLabel JTextField JButton)
              (java.awt.event ActionListener)
              (java.awt GridLayout)))

    (defn celsius
      [c]
      [(+ 32 (* 1.8 c)) "Fahrenheit"])

    (defn frame-created
      [event]
      (let [frame (slix-frame)
            temp-text (JTextField.)
            celsius-label (JLabel. "Celsius")
            convert-button (JButton. "Convert")
            fahrenheit-label (JLabel. "Fahrenheit")]
        (.addActionListener convert-button
                            (proxy [ActionListener] []
                              (actionPerformed [evt]
                                (let [c (Double/parseDouble (.getText temp-text))]
                                  (.setText fahrenheit-label
                                            (apply print-str (celsius c)))))))
        (doto frame
          (.setLayout (GridLayout. 2 2 3 3))
          (.add temp-text)
          (.add celsius-label)
          (.add convert-button)
          (.add fahrenheit-label)
          (.setSize 300 80))))

    (defn opened
      [event]
      (set-slix-visible))

    (defn saving
      [event]
      (event-response-donot-save))
</code></pre>

<h4>4.7 Interacting with Slix</h4>

<p>You can inspect slix instance at the REPL interactively. Open "Cc" and call the <code>repl</code> function with the instance name like this:</p>

<pre><code>(repl Cc)
</code></pre>

<p>Note that you don't need to quote the name. A REPL window opens and the command prompt indicates that you are in the <code>slix.cc</code> namespace. Also *slix* is set to the instance value. Just type <code>*slix*</code> at the REPL and take a look at the key-values. There are the Sevenri functions that refer to *slix* by default and return a value corresponding to the function name as key. For example, the Sevenri functions <code>slix-on</code>, <code>slix-name</code>, and <code>slix-frame</code> return slix name, slix instance name, and JFrame of slix instance respectively. Those functions also accept a slix instance value as argument. Try these at the REPL and you should get the same results.</p>

<pre><code>    (slix-name)
    (slix-name *slix*)
    (slix-name (get-slix 'Cc))
</code></pre>

<p><img src="../res/ss-dev-slix6.png" alt="Interacting with olix" title="Interacting with slix" /></p>

<p>You can get the JFrame of the current slix instance using the <code>slix-frame</code> function and make changes to the window directly at the REPL. First, get the JFrame.</p>

<pre><code>    (slix-frame)
</code></pre>

<p>Then get the Container in the JFrame:</p>

<pre><code>    (.getContentPane *1)
</code></pre>

<p>Then the the JTextField added as the first component to the container:</p>

<pre><code>    (.getComponent *1 0)
</code></pre>

<p>Finally set a new Celsius value to the JTextField.</p>

<pre><code>    (.setText *1 "50")
</code></pre>

<p>Or, use this one liner that runs the whole steps above.</p>

<pre><code>    (-&gt; (slix-frame) (.getContentPane) (.getComponent 0) (.setText "50"))
</code></pre>

<p>One important rule of Swing is that Swing components can be accessed by only one thread at a time, and normally the thread is the event dispatch thread (the EDT for short). In accordance with the rule you can invoke the above one linear in the EDT using the <code>invoke-later</code> function.</p>

<pre><code>    (invoke-later #(-&gt; (slix-frame) (.getContentPane) (.getComponent 0) (.setText "50")))
</code></pre>

<p>Notice that *slix* is available in the EDT too because the <code>invoke-later</code> function sets it up so that the <code>slix-frame</code> function still works.</p>

<p>Now let's change "Cc" to calculate Fahrenheit to Celsius instead of Celsius to Fahrenheit. First, change the label "Celsius" to "Fahrenheit".</p>

<pre><code>    (-&gt; (slix-frame) (.getContentPane) (.getComponent 1) (.setText "Fahrenheit"))
</code></pre>

<p>Then redefine the <code>celsius</code> function to convert Fahrenheit to Celsius. You can do it at the REPL as well, but let's do in Ced. Back to the Ced window and change the <code>celsius</code> function like this:</p>

<pre><code>    (defn celsius
      [c]
      [(* (- c 32) 5/9) "Celsius"])
</code></pre>

<p>Press META+S then F2 to load the lib file and update the <code>celsius</code> function. Bring the "Cc" window to the front. If the window still has "50" in the JTextField as it was set above, you'll see "10.0 Celsius" when you click the <code>Convert</code> button.</p>

<p>You can modify running instance interactively and try out new code incrementally this way.</p>

<p><img src="../res/ss-dev-slix7.png" alt="Interacting with olix #2" title="Interacting with slix #2" /></p>

<h4>4.8 The purge-slix Function</h4>

<p>Lastly, when you no longer need a slix and want to remove not only its instances but also the slix itself, use the <code>purge-slix</code> function with slix name. Call the function, for example, to purge <code>cc</code> like this:</p>

<pre><code>    (purge-slix 'cc)
</code></pre>

<p>In case you want to retrieve trashed slix files, look into the directories <code>.sevenri/trash/src/slix/</code><em>slix-name</em> for lib files and <code>.sevenri/trash/sid/slix/</code><em>slix-name</em> for saved instance files.</p>

<h4>Appendix A: Per-slix ClassLoader</h4>

<p>Each slix instance has a ClassLoader that is used to locate slix specific classes. By convention slix specific class and jar files for slix <em>X</em> are saved in the directory <code>src/slix/</code><em>X</em><code>/jvm</code>. The ClassLoader is also used to locate aot-compiled classes of slix <em>X</em> and they are saved in the directory <code>.sevenri/classes/slix/</code><em>X</em>. The parent ClassLoader of per-slix ClassLoader locates libs and classes global to Sevenri. So the ClassLoader stack of slix <em>X</em> should look like below:</p>

<blockquote>
  <p><em>Path-to-the-Sevenri-directory</em><code>/src/slix/</code><em>X</em><code>/jvm/</code> <br />
  <em>Path-to-the-Sevenri-directory</em><code>/.sevenri/classes/</code> <br />
  <em>Path-to-the-Sevenri-directory</em><code>/src/</code> <br />
  <em>Path-to-the-Sevenri-directory</em><code>/lib/clojure.jar</code> <br />
  <em>Path-to-the-Sevenri-directory</em><code>/lib/clojure-contrib.jar</code> <br />
  --- more Sevenri lib .jar lines here --- <br />
  --- Java system .jar lines here ---  </p>
</blockquote>

<p>You can print actual ClassLoader stack using the <code>print-cl</code> function at the REPL.</p>

<p>So, for example, if you want to use <em>Y.jar</em> for slix <em>X</em>, put the jar file in the directory <code>src/slix/X/jvm</code>. Then you can refer to the class <em>Z</em> in the package <em>Y</em> like "Y.Z".</p>